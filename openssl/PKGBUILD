# Maintainer: Alexey Pavlov <alexpux@gmail.com>

pkgname=('openssl' 'libopenssl' 'openssl-devel')
_ver=1.1.0h
# use a pacman compatible version scheme
pkgver=${_ver/[a-z]/.${_ver//[0-9.]/}}
pkgrel=1
pkgdesc='The Open Source toolkit for Secure Sockets Layer and Transport Layer Security'
arch=('i686' 'x86_64')
url='https://www.openssl.org'
license=('custom:BSD')
depends=('zlib')
makedepends=('gcc' 'tar' 'perl' 'zlib-devel' 'diffutils' 'nasm')
options=('!makeflags' 'staticlibs')
source=("https://www.openssl.org/source/${pkgname}-${_ver}.tar.gz"{,.asc}
        'no-rpath.patch'
        'ca-dir.patch'
        'openssl-1.0.1e-cygwin64.patch'
        'openssl-1.0.1e-msys2.patch'
        'openssl-1.1.0h-msys2.patch'
        'openssl-1.0.0-beta5-enginesdir.patch')
sha256sums=('5835626cde9e99656585fc7aaa2302a73a7e1340bf8c14fd635a62c66802a517'
            'SKIP'
            '754d6107a306311e15a1db6a1cc031b81691c8b9865e8809ac60ca6f184c957c'
            '7b188f51d7a6976e24237db93f21748e807c2ba7e2163cf3c496e4d8be12fb02'
            'e82f015bd01f73bb3840eaf6ad5e00b62399ea38f924372471d00a9782d56b1a'
            '7ddfe47d4b582cf5085ae20f2a2db0f2b87e0cd1a91885cf1f76ff1a2219bf40'
            '57d1e13f40e3bc70886104b69dd8426d9125966d470f7664248c5772e703e69f'
            '1ce6d3d795124f4a0206790f21d178049fb6d56945820582779a0d0e14dbf230')
validpgpkeys=('8657ABB260F056B1E5190839D9C4D26D0E604491')

prepare() {
  #[[ -d ${srcdir}/${pkgname}-${_ver} ]] || tar -xzvf ${srcdir}/${pkgname}-${_ver}.tar.gz -C ${srcdir}
  cd ${srcdir}/${pkgname}-${_ver}

  # remove rpath: https://bugs.archlinux.org/task/14367
  #patch -p0 -i ${srcdir}/no-rpath.patch
  # set ca dir to /etc/ssl by default
  patch -p1 -i ${srcdir}/ca-dir.patch
  patch -p1 -i ${srcdir}/openssl-1.1.0h-msys2.patch

  #patch -p1 -i ${srcdir}/openssl-1.0.1e-cygwin64.patch
  #patch -p1 -i ${srcdir}/openssl-1.0.1e-msys2.patch
  #patch -p1 -i ${srcdir}/openssl-1.0.0-beta5-enginesdir.patch
}

build() {
  cd ${srcdir}/${pkgname}-${_ver}

  if [ "${CARCH}" == 'x86_64' ]; then
    openssltarget='Msys-x86_64'
  elif [ "${CARCH}" == 'i686' ]; then
    openssltarget='Msys-x86'
  fi

  ./Configure \
    --prefix=/usr \
    --openssldir=/usr/ssl \
    --enginesdir=/usr/lib/openssl/engines \
    --libdir=lib \
    shared \
    zlib \
    no-idea \
    no-rc5 \
    ${openssltarget}

  make depend
  make all
  make DESTDIR=${srcdir}/dest MANDIR=/usr/share/man MANSUFFIX=ssl install_sw install_ssldirs install_man_docs
}

check() {
  cd ${srcdir}/${pkgname}-${_ver}

  make test
}

package_openssl() {
  depends=('libopenssl' 'zlib')
  optdepends=('ca-certificates' 'perl')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/bin/*.exe ${pkgdir}/usr/bin/
  cp -f ${srcdir}/dest/usr/bin/c_rehash ${pkgdir}/usr/bin/
  cp -rf ${srcdir}/dest/usr/share ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/ssl ${pkgdir}/usr/
  install -D -m644 ${srcdir}/${pkgname}-${_ver}/LICENSE ${pkgdir}/usr/share/licenses/${pkgname}/LICENSE
}

package_libopenssl() {
  depends=('zlib')
  groups=('libraries')

  mkdir -p ${pkgdir}/usr/bin
  cp -f ${srcdir}/dest/usr/lib/*.dll ${pkgdir}/usr/bin/

  mkdir -p ${pkgdir}/usr/lib/openssl
  cp -rf ${srcdir}/dest/usr/lib/openssl/engines ${pkgdir}/usr/lib/openssl/
  chmod -R 755 ${pkgdir}/usr/lib/openssl/engines
}

package_openssl-devel() {
  pkgdesc="Openssl headers and libraries"
  groups=('development')
  depends=("libopenssl=${pkgver}" 'zlib-devel')

  mkdir -p ${pkgdir}/usr/lib
  cp -rf ${srcdir}/dest/usr/include ${pkgdir}/usr/
  cp -rf ${srcdir}/dest/usr/lib/pkgconfig ${pkgdir}/usr/lib/
  cp -f ${srcdir}/dest/usr/lib/*.a ${pkgdir}/usr/lib
}
